; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{BDD60DE7-9374-463C-8E74-8227EB03E28F}
AppName=Dash Faction
AppVersion=1.4.2
;AppVerName=Dash Faction 1.4.2
AppPublisher=rafalh
AppPublisherURL=https://rafalh.tk/
AppSupportURL=https://rafalh.tk/
AppUpdatesURL=https://rafalh.tk/
DefaultDirName={commonpf}\Dash Faction
DefaultGroupName=Dash Faction
LicenseFile=LICENSE.txt
InfoBeforeFile=README.md
OutputBaseFilename=DashFaction-setup
Compression=lzma
SolidCompression=yes
OutputDir=build

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "rfproto"; Description: "Register rf:// protocol handler"; GroupDescription: "Other options:"
Name: "fftracker"; Description: "Set rfgt.factionfiles.com as multiplayer tracker"; GroupDescription: "Other options:"

[Files]
Source: "build\Release\bin\DashFactionLauncher.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\bin\CrashHandler.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\bin\DashEditor.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\bin\DashFaction.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\bin\dashfaction.vpp"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\Dash Faction"; Filename: "{app}\DashFactionLauncher.exe"
Name: "{commondesktop}\Dash Faction"; Filename: "{app}\DashFactionLauncher.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\DashFactionLauncher.exe"; Description: "{cm:LaunchProgram,Dash Faction}"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCU; Subkey: "Software\Volition\Red Faction\Dash Faction"; ValueType: "string"; ValueName: "Executable Path"; ValueData: "{code:GetRFExePath}"
Root: HKCU; Subkey: "Software\Volition\Red Faction"; ValueType: "string"; ValueName: "GameTracker"; ValueData: ""
Root: HKCR; Subkey: "rf"; ValueType: "string"; ValueData: "URL:Red Faction Protocol"; Flags: uninsdeletekey; Tasks: rfproto
Root: HKCR; Subkey: "rf"; ValueType: "string"; ValueName: "URL Protocol"; ValueData: "rfgt.factionfiles.com"; Tasks: fftracker
Root: HKCR; Subkey: "rf\DefaultIcon"; ValueType: "string"; ValueData: "{app}\DashFactionLauncher.exe,0"; Tasks: rfproto
Root: HKCR; Subkey: "rf\shell\open\command"; ValueType: "string"; ValueData: """{app}\DashFactionLauncher.exe"" -game -url %1"; Tasks: rfproto

[CustomMessages]
RFExeLocation=Please select Red Faction executable file.%nFor installation from Steam it is usually: C:\Program Files\Steam\steamapps\common\red faction\RF.exe%nFor installation from CD (retail) it is usually: C:\games\RedFaction\RF.exe%nNote: Dash Faction requires 1.20-NA version of this file. You may have to replace the file if you have uncompatible version. Find more help at https://redfaction.help%n%n&Location of RF.exe:

[Code]
var
  SelectRFExePage: TInputFileWizardPage;

procedure InitializeWizard;
begin
  SelectRFExePage := CreateInputFilePage(wpSelectDir, 'Select RF.exe', 'Select Red Faction executable file', '');
  SelectRFExePage.Add(ExpandConstant('{cm:RFExeLocation}'), 'Executable files|*.exe|All files|*.*', '.exe');
  SelectRFExePage.Values[0] := ExpandConstant('C:\games\RedFaction\RF.exe');
end;

function GetRFExePath(Param: string): String;
begin
  Result := SelectRFExePage.Values[0];
end;
